// Instructions
// - Set e_PlayerSkills to your players in .ini
// - Configure your values in the defname table.
// - Use f_SkillWait to set up a delay between skills.

// Skill waiting time between uses.
// Order = 1=Select, 2=Success, Fail, Others
// 0.0 reset, -1 no action.
// SkillWait_Cancel is used when you cancel after @Select
[DEFNAME defSkillsWaitingTimes]
SkillWait_Cancel			2.0, 2.0, 2.0

SkillWait_Anatomy			3.0, 2.0, 2.0
SkillWait_AnimalLore		3.0, 2.0, 2.0
SkillWait_Begging			3.0, 3.0, 3.0
SkillWait_Cartography		3.0, 3.0, 3.0
SkillWait_EvaluatingIntel	2.0, 2.0, 2.0
SkillWait_Taming			21600.0, 3.0, 3.0
SkillWait_Tracking			5.0, 3.0, 3.0
SkillWait_Mining			5.0, 3.0, 3.0

// Usage: f_SkillWait action,skill
// Example: f_SkillWait 1,Begging
// Adds delay after Success in Begging
// 0 = Start, 1 = Success, Fail, Others
[FUNCTION f_SkillWait]
if !<isplayer>
return 0
endif
if (<argv[0]> != -1)
	if (strmatch(Cancel,<argv[1]>))
		local.skill = Cancel
	else
		local.skill = <serv.skill.<argv[1]>.key>
	endif
local.time = <f_GetARGV <argv[0]> <def.SkillWait_<local.skill>>>
ctag.SkillRecovery = <eval (<serv.time> + <local.time>)>
	if <serv.time> > <ctag0.SkillRecovery>
		ctag.SkillRecovery =
	endif
else
ctag.SkillRecovery =
endif

// Event to add to every player
[EVENTS e_PlayerSkills]
ON=@TargOn_Cancel
if !<isempty <args>>
	f_SkillWait 0,Cancel
	f_BardDelay 2.0
endif

ON=@SkillWait
if (<flags>&statf_freeze) || (<flags>&statf_stone) || (<flags>&statf_dead) || (<tag0.nomovetill> >= <serv.time>)
	src.sysmessage <defmsg.skillwait_1>
	return 1
endif
if ((<argn1> == 9) || (<argn1> == 22) || (<argn1> == 15))
	if <ctag0.BardRecovery> > <serv.time>
		src.sysmessage <defmsg.skillwait_3>
		return 1
	endif
else
	if <ctag0.SkillRecovery> > <serv.time>
		src.sysmessage <defmsg.skillwait_3>
		return 1
	endif
endif

ON=@SpellSelect
if <tag0.NoCastTill> > <serv.time>
	src.sysmessage Debes esperar antes de ejecutar esta acci√≥n.
	return 1
endif