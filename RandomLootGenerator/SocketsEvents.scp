// *************************************************************
// SphereServerX @ UOAscension @ 2019
// This script is part from the Sphere Community Script Pack.
// Download the full pack from SphereCommunity.net
// ************************************************************
VERSION=X1

// Slayer bonus
// Luck and random loot generation
// We have two types of luck loot.
// TAG.Luck.Loot which is defined by the tag and chances are Luck @ 0.44
// Generated loot which can be TAG.LuckItems or according to monster level.
[EVENTS e_NPCSockets]
ON=@GetHit
if <slayer> != 0
	local.WeaponSlayer = <src.f_Slayer_GetWeapon>
	if <local.WeaponSlayer> != 0
		if <slayer> == <local.WeaponSlayer>
			argn1 += <muldiv <argn1>,25,100>
		else
			argn1 -= <muldiv <argn1>,15,100>
		endif
	endif
	local.TalismanSlayer = <src.f_Slayer_GetTalisman>
	if <local.TalismanSlayer> != 0
		if <slayer> == <local.TalismanSlayer>
			argn1 += <muldiv <argn1>,10,100>
		endif
	endif
endif

ON=@DeathCorpse
local.corpse = <argo.uid>
ref5 = <attacker.max>
ref5 = <qval <ref5.owner> ? <ref5.owner.uid> : <ref5.uid>>
ref5 = <qval ((<ref5.isevent.e_BardProvoked>) && (<ref5.tag0.BardProvokeLink>)) ? <ref5.tag0.BardProvokeLink> : <ref5.uid>>
forcont <argo.uid> 2
	if <baseid> == i_gold
		local.gold += <amount>
	endif
endfor
if <ref5.IncreaseGold>
	local.ToAdd = <muldiv <local.gold>,<ref5.IncreaseGold>,100>
	if <local.ToAdd> < 1
		local.ToAdd = 1
	endif
	local.gold += <local.ToAdd>
	newitem i_gold,<local.ToAdd>,<argo.uid>
endif
// Up to here, working perfectly
local.level = <max 0,<eval <local.gold> / 800>>
local.chances = <feval <floatval <ref5.Luck> @ 0.44>>
if <tag0.LuckItems>
	local.LuckItems = <eval <r<eval <tag0.LuckItems> / 2>,<tag0.LuckItems>>>
else
local.LuckItems = 0
	if <r5> == 1
		local.LuckItems = 1
	endif
	for 1 <local.level>
		local.rand = <r100>
		if <local.chances> >= <local.rand>
			local.LuckItems ++
		endif
	endfor
endif
doswitch <local.level>
	local.LuckProps = 1
	local.LuckProps = <r1,2>
	local.LuckProps = 2
	local.LuckProps = <r2,3>
enddo
if <local.LuckItems> >= 1
	for 1 <local.LuckItems>
		args = <def.ItemProps>
		local.ItemType = <argv[<r0,<eval <argv> -1>>]>
		newitem <def.random_luckloot_<local.ItemType>>,1,<local.corpse>
		if (<local.LuckProps> > <def.ItemProp<local.ItemType>Amount>)
			local.LuckProps = <def.ItemProp<local.ItemType>Amount>
		endif
		new.tag0.Faction = <slayer>
		new.f_GenerateProps <local.ItemType>,<local.LuckProps>,<local.Chance>
	endfor
endif

[EOF]